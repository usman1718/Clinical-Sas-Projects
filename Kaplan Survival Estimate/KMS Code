*********** Kaplan Meier Survival Estimates for Death over Time********;
*********Input Small part of an ADAM ADTTE analysis dataset*******;

data ADTTE;
	label TRTA = "Actual Treatment"
		  AVAL = "Analysis Value"
		  CNSR = "Censor";
	input TRTA $ AVAL  CNSR @@;
	
Datalines;
A  543    0     A  453    1     A  453    1     A  7436   1
B  232	  1     C  43     0     C  8790   0     B  154    1
C  34	    1	    B  435    1     B  7621   1     C  542    0
A  23	    0     A  323    0     C  8754   1     C  334	  0
B  23	    0     A  34     1     A  212	  0     A  145    1
C  4532   1     B  982    0     B  009    1     C  524    0
B  2323   0     C  76     1     A  874    1     B  321    1
C  109	  1     B  465    1     B  2323   0     A  288    0
A  232	  1     A  653    0     C  8754   1     B  982    0
B  4545   0     C  873    1     A  067    0     A  265    1
A  678	  1     B  980    0     A  232	  1     C  334	  0
A  32	    0     A  874    1     C  732    1     A  289    1
B  221	  1     B  900    0     B  334    0     B  982    0
C  678	  1     A  874    0     A  212	  0     C  144    1
A  345	  0     C  003    1     B  833    1     A  3224   0
B  234	  1     B  009    1     C  231    1     B  231    1
C  799	  1     B  643    1     A  212	  0     A  123    0
A  3224   0     C  100	  0     C  342    1     C  334    0
A  23	    0     C  334	  0     B  3233   0     B  32     1
A  878	  1     A  212	  0     A  232	  1     C  32     0
B  444	  0     C  992	  0     B  0338   0     B  321    1
C  100	  1     B  579    1     C  3232   1     C  435    0
C  212	  0     C  383    1     A  212	  0     A  87     1
B  2323   1     B  54	    0     C  8754   1     A  3224   0
C  43	    1     A  436    1     B  7621   1     B  324    9
B  643	  1     C  870    1     C  3223   0     C  298    1
A  77	    0     C  870    1     A  232	  1     B  324    0
B  889	  1     B  865    0     C  8754   1     C  334	  0
C  334	  0     A  866    1     B  7621   1     A  342    1
B  66	    1     A  212	  0     C  2382   0     B  536    1
A  777	  0     B  902    1     A  212	  0     C  298    1
C  32	    1     C  782    1     B  123    1     A  234    0
B  54	    0     B  589    0     B  982    1     A  3224   0
B  356	  1     A  329    1     C  334	  0     C  298    1
A  323	  0     B  2323   0     C  873    1     B  321    1
C  980	  1     A  589    0     A  981    0     C  196    1
A  212	  0     C  565    1     A  212	  0     A  920    0
B  44	    1     B  321    1     C  334	  0     B  321    1
C  546	  0     C  992	  0     B  7621   1     C  1672   0
A  55	    1     A  874    1     B  732    1     A  3224   0
B  889	  0     B  2323   0     A  323    0     B  192    0
A  434	  1     C  789    1     C  132    0     C  837    1
C  76	    0     A  568    1     C  334	  0     B  982    0
A  3223   1     C  876    0     A  635    1     C  323    1
B  3345   0     A  874    1     B  324    0     A  232    1
B  231	  1     C  768    0     A  873    1     B  321    1
C  543	  0     B  54	    0     B  430    0     B  982    0
A  789	  1     A  256    1     C  334	  0     C  334    0
C  992	  0     C  870    1     A  873    1     A  321    1
;
run;

*****Perform livetest and export survival estimates to survest data set******;
ods listing close;
ods output ProductLimitEstimates = survivalest;
proc lifetest
	data = adtte;
	time aval*cnsr(1);
	strata trta;
	run;
ods output close;
ods listing;

data survivalest;
	set survivalest;
	
	********Calculate visit Window Months*****;
if aval = 0 then
	visit = 0;  *****baseline***;
else if 1 <= aval <= 91 then
	visit = 91;  *******3 months***;
else if 92 <= aval <= 183 then
	visit = 183;  *******6 months***;
else if 184 <= aval <= 365 then
	visit = 365;  *******1 Year***;
else if 366 <= aval <= 731 then
	visit = 731;  *******2 years***;
else if 732 <= aval <= 1096 then
	visit = 1096;  *******3 years***;
else if 1097 <= aval <= 1461 then
	visit = 1461;  *******4 years***;
else 
put "ERR" "OR: event data beyond visit mapping"
	aval = ;
run;

proc sort 
data = survivalest;
by trta visit aval;
run;

******Create 95% confidence interval around the estimate and retain proper
survival estimate for table*******;
data survivalest;
	set survivalest;
	by trta visit aval;
keep trta visit count left survprob lcl ucl;
retain count survprob lcl ucl;


******initialize vairables to missing for each treatment*****;
if first.trta then
	do;
		survprob = .;
		count = .;
		lcl = .;
		ucl = .;
		end;
		
******create carryforward observations with an estimate.;
if survival ne . then 
	do;
		count = failed;
		survprob = survival;
****** supress confidence interval at baseline*******;
if visit ne 0 and stderr ne . then
do;
	lcl = survival - (stderr*1.96);
	ucl = survival + (stderr*1.96);
end;
end;

******Keep one record per visit window*****;
if last.visit;
run;

proc sort 
data = survivalest;
	by visit;
run;

*****Colapse table by treatment . this is done by merging the survivalest data set 
against itself 3 times*******;
data table;
	merge survivalest
		(where=(trta="A")
		rename = (count=count_a left=left_a
				   survprob = survprob_a lcl=lcl_a ucl=ucl_a))
		  survivalest
		(where=(trta="B")
		rename = (count=count_b left=left_b
				   survprob = survprob_b lcl=lcl_b ucl=ucl_b))
		  survivalest
		(where=(trta="C")
		rename = (count=count_c left=left_c
				   survprob = survprob_c lcl=lcl_c ucl=ucl_c)) ;
		by visit;
		run;
		
*****Create visit format used in table.*****;
proc format ;
	value visit
		0 = "Baseline"
		91 = "3 Months"
		183 = "6 Months"
		365 = " 1 Year "
		731 = " 2 Years"
		1096 = " 3 Years"
		1461 = " 4 Years";
	run;
	
******* Create summary with rpoc report *******;
options nodate nonumber missing = ' ';
ods escapechar = '#';
ods pdf style = htmlblue file ='Programreport.pdf';

proc report 
data = table
nowindows
split = "|";

columns (visit 
		("Placebo " count_a left_a survprob_a
					("95% CLs" lcl_a ucl_a))
		("Old Drug" count_b left_b survprob_b
					("95% CLs" lcl_b ucl_b))
		("New Drug" count_c left_c survprob_c
					("95% CLs" lcl_c ucl_c)));
					
define visit        /order order = internal "Visit" left
					 format = visit.;
define count_a		/display "Cum. Deaths" width = 6
					 format = 3. center;
define left_a		/display "Remain at risk" width = 6 
					 format = 3. center spacing = 0;
define survprob_a	/display "Surv-ival Prob." center 
					 format = pvalue5.3;
define lcl_a		/display "lower" format = 5.3;
define ucl_a		/display "Upper" format = 5.3;

define count_b		/display "Cum. Deaths" width = 6
					 format = 3. center;
define left_b		/display "Remain at risk" width = 6 
					 format = 3. center spacing = 0;
define survprob_b	/display "Surv-ival Prob." center 
					 format = pvalue5.3;
define lcl_b		/display "lower" format = 5.3;
define ucl_b		/display "Upper" format = 5.3;

;
define count_c		/display "Cum. Deaths" width = 6
					 format = 3. center;
define left_c		/display "Remain at risk" width = 6 
					 format = 3. center spacing = 0;
define survprob_c	/display "Surv-ival Prob." center 
					 format = pvalue5.3;
define lcl_c		/display "lower" format = 5.3;
define ucl_c		/display "Upper" format = 5.3;

break after visit/ skip;

title1 j=l 'Medcomplytics / RXT-17'
       j=r 'Page 1 of 7';
title2 j=c 'Table 1.4';
title3 j=c 'Kaplan-Meier Survival Estimates For Death Over Time';
footnote1 " Created by Usman Khan on 28-12-2022";
run;

ods pdf close;
