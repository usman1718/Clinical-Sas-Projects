data adsl;
length USUBJID $ 3;
label USUBJID = "Unique Subject Identifier"
	  TRTPN = "Planned Treatment (N) ";
input USUBJID $ TRTPN @@ ;
datalines;
101 1  102 0  103 0  104 1  105 1  106 0  107 1  108 1  109 0  110 1
111 0  112 1  113 0  114 1  115 0  116 1  117 0  118 0  119 1  120 0
121 1  122 0  123 1  124 1  125 0  126 1  127 0  128 1  129 0  130 0
131 0  132 1  133 0  134 1  135 1  136 0  137 1  138 0  139 1  140 0
141 1  142 0  143 1  144 0  145 1  146 0  147 1  148 1  149 0  150 1
151 0  152 1  153 0  154 1  155 1  156 0  157 1  158 0  159 1  160 0
161 1  162 0  163 1  164 0  165 1  166 0  167 1  168 0  169 0  170 1
;
run;

*******Input Sample Concomitant Medication data as SDTM CM domain*******;
data CM;
label USUBJID = "Unique Subject Identifier"
	  CMDECOD = "Standardized Medication Name";
input USUBJID $ 1-3 CMDECOD $ 5-27;
datalines;
101 ACETYLSALICYLIC ACID
101 HYDROCORTISONE 
102 VICODIN
102 POTASSIUM
102 IBUPROFEN
103 MAGNESIUM SULFATE
103 RINGER-LACTATE SOLUTION
115 LORAZEPAM
115 SODIUM BICARBONATE
116 POTASSIUM
117 MULTIVITAMIN
117 IBUPROFEN
119 IRON
130 FOLIC ACID
131 GABAPENTIN
132 DIPHENHYDRAMINE
135 SALMETEROL
140 HEPARIN
140 HEPARIN
140 NICOTINE
141 HYDROCORTISONE
141 IBUPROFEN
;

****Perform a simple count of each treatment arm and output results*****;
**** As Macro Variables. N1(active) ,N2(placebo),N3(total)*******;
proc sql noprint;

******for active********;
select count(distinct usubjid) format = 3.
	into :n1
	from adsl
	where trtpn = 1;
	
*******for placebo*******;
select count(distinct usubjid) format = 3.
	into :n2
	from adsl
	where trtpn = 0;
	
****for total******;
select count(distinct usubjid) format = 3.
	into :n3
	from adsl
	where trtpn ne .;
quit;
	
******Merge Concomitant Medication And Treatment Data******;
******Keep Records for subjects who had conmeds and study therapy*****;
******get unique concomitant medications within patient*******;
proc sql noprint;
	create table cmtosum as 
		select unique(c.cmdecod) as cmdecod , c.usubjid,t.trtpn
			from cm as c , adsl as t
			where c.usubjid = t.usubjid
			order by usubjid , cmdecod;
			quit;
			
******* get medication counts by treatment and place in dataset counts****;
*****turn off list ouput*****;
ods listing close;

*****send sums by treatment to counts data set*****;
ods output CrossTabFreqs = counts;
proc freq
	data = cmtosum;
		tables trtpn * cmdecod;
		run;
ods output close;
ods listing;

proc sort
data = counts;
by cmdecod;
run;

***Merge Counts Dataset with itself to put the three treatment column
side by side to each conmed. Create group variable which are used to create break line
in the report. define COL1-COL3 which are the count/% formatted columns****;

data cm;
	merge counts(where = (trtpn=1) rename = (frequency = count1))
		  counts(where = (trtpn=0) rename = (frequency = count2))
		  counts(where = (trtpn=.) rename = (frequency = count3))
		  end = eof;
	by cmdecod;
	
	keep cmdecod rowlabel col1-col3 section;
	length rowlabel $ 25 col1-col3 $ 10;
	
*****Label "Any Medication" row and put in first group. by medication count
goes into second group.*****;

if cmdecod = '' then
 do;
	 rowlabel = "Any Medication";
		 section = 1;
		 end;
	else 
	do;
		rowlabel = cmdecod;
			section = 2;
		end;
		
*****Calculate Percentages and create N/% Text in Col1-Col3.;
	pct1 = (count1/ &n1) * 100;
	pct2 = (count2/ &n2) * 100;
	pct3 = (count3/ &n3) * 100;
	
	col1 = put(count1,3.)  || " (" || put (pct1, 3.) || "%)";
	col2 = put(count2,3.)  || " (" || put (pct2, 3.) || "%)";
	col3 = put(count3,3.)  || " (" || put (pct3, 3.) || "%)";
run;

****use proc report to write the conmed table to file****;
options nodate nonumber missing = ' ';
ods escapechar = '#';
ods pdf style = htmlblue file ='conmed5.3.pdf';

proc report
data = cm
nowindows
split = "|";

columns section rowlabel col1 col2 col3 ;

define section   /order order = internal noprint;
define rowlabel  /order width=25 "preferred Medication Term";
define col1		 /display center width=14 "Active|N =&n1";
define col2		 /display center width=14 "Placebo|N =&n2";
define col3		 /display center width=14 "Total|N =&n3";

compute after section;
	line '#{newline}';
endcomp;

title1 j=l 'Medcomplytics / RXTRT-1'
	   j=r 'page 1 of 2';
title2 j=c 'Table 5.3';
title3 j=c ' Summary of Concomitant Medication/Prior Medication';
run;
ods pdf close;

	
